subroutine solve_matrix_n(my_id,i_tor,MPI_COMM_N,MPI_COMM_MASTER,solve_only)
!---------------------------------------------------------------------
! subroutine solves the system of equation for each harmonic
! using mumps with centralised matrix on the group mpi_group_n (mpi_comm_n)
!---------------------------------------------------------------------
use parameters

use mumps_module
use pastix_module

use global_distributed_matrix
implicit none
include 'mpif.h'

integer :: i, my_id, i_tor(*), i_reduced, j_reduced, n_i, n_j, index, index1, index2
integer :: MPI_COMM_N, MPI_COMM_MASTER, my_id_n, n_cpu_n, ierr, my_id_master, n_cpu_master
real*8  :: t_analysis_0, t_analysis_1, t_fact_0, t_fact_1, t_solv_0, t_solv_1
real*8, allocatable :: RHS_tmp(:)
logical :: solve_only

integer, external :: omp_get_num_threads, omp_get_thread_num

write(*,*) my_id,'*********************************'
write(*,*) my_id,'*      solve local matrix  (n)  *'
write(*,*) my_id,'*********************************'

if (use_mumps)  write(*,*) my_id,'*       using solver MUMPS      *'
if (use_pastix) write(*,*) my_id,'*       using solver PastiX     *'

write(*,*) my_id,'*********************************'

call MPI_COMM_RANK(MPI_COMM_N, my_id_n, ierr)     ! the id of each cpu
call MPI_COMM_SIZE(MPI_COMM_N, n_cpu_n, ierr)     ! the number of cpus

if (my_id_n .eq. 0) then
  call MPI_COMM_RANK(MPI_COMM_MASTER, my_id_master, ierr)     ! the id of each cpu
  call MPI_COMM_SIZE(MPI_COMM_MASTER, n_cpu_master, ierr)     ! the number of cpus
endif


if (.not. solve_only) then

  if (use_mumps) then

    mumps_par%JOB = 1                                 ! Analysis, only needed when grid has changed
    call cpu_time(t_analysis_0)

    mumps_par%icntl(7)  = 4                            ! reorderign option (7:automatic, 3:Scotch, 4:PORD, 5:METIS)
    mumps_par%icntl(8)  = 7                            ! row and column scaling
    mumps_par%icntl(14) = 30                           ! MAXS
    mumps_par%icntl(18) = 0

    call DMUMPS(mumps_par)

    call cpu_time(t_analysis_1)

    if (my_id_n .eq.0) write(*, '(i3,A,f8.3)') my_id,' MUMPS, analysis  : ',t_analysis_1-t_analysis_0

  else

    if (my_id_n .eq. 0) then

      if (allocated(sparskit_work)) deallocate(sparskit_work)
      allocate(sparskit_work(mumps_par%N + 1))

      call coicsr(mumps_par%N,mumps_par%NZ,1,mumps_par%A,mumps_par%IRN,mumps_par%JCN,sparskit_work)

      deallocate(sparskit_work)

    endif
    
    if (use_pastix) then
     
      if (.not. pastix_smp_only) then
      
!$omp parallel default(none) shared(pastix_nthrd)    
        pastix_nthrd = omp_get_num_threads()
!$omp end parallel

        write(*,'(i5,A,i5)') my_id,' PastiX n_threads : ',pastix_nthrd 

        call MPI_BCAST(mumps_par%n,1,MPI_INTEGER,0,MPI_COMM_N,ierr)
        call MPI_BCAST(mumps_par%nz,1,MPI_INTEGER,0,MPI_COMM_N,ierr)
    
        if (my_id_n .gt. 0) then
          if (associated(mumps_par%irn)) deallocate(mumps_par%irn)
          if (associated(mumps_par%jcn)) deallocate(mumps_par%jcn)
          if (associated(mumps_par%A))   deallocate(mumps_par%A)
          if (associated(mumps_par%rhs)) deallocate(mumps_par%rhs)
          allocate(mumps_par%irn(mumps_par%nz),mumps_par%jcn(mumps_par%nz),mumps_par%a(mumps_par%nz),mumps_par%rhs(mumps_par%n))
        endif

        call MPI_BCAST(mumps_par%IRN,mumps_par%nz,MPI_INTEGER,0,MPI_COMM_N,ierr)
        call MPI_BCAST(mumps_par%JCN,mumps_par%nz,MPI_INTEGER,0,MPI_COMM_N,ierr)
        call MPI_BCAST(mumps_par%A,mumps_par%nz,MPI_DOUBLE_PRECISION,0,MPI_COMM_N,ierr)
  
      endif

      if  (.not. pastix_initialised)  then
    
        if ( (.not. pastix_smp_only) .or. (pastix_smp_only .and. (my_id_n .eq.0)) ) then
      
!          if (pastix_smp_only) pastix_nthrd = n_cpu_n                ! use the size of the MPIgroup for the number of threads

!$omp parallel default(none) shared(pastix_nthrd)    
          pastix_nthrd = omp_get_num_threads()
!$omp end parallel

          pastix_iparm(1)     = 0                                     ! insert default values
          pastix_iparm(2)     = 0                                     ! initializse
          pastix_iparm(3)     = 0

          pastix_iparm(31) = pastix_facto
          pastix_iparm(35) = pastix_nthrd                             ! thread/mpi
          pastix_iparm(39) = pastix_rhs
          pastix_iparm(41) = pastix_sym

          pastix_iparm(42) = pastix_ricar
          pastix_iparm(37) = pastix_iluk
          pastix_iparm(14) = pastix_amalg

          if (.not. pastix_smp_only) call MPI_BCAST(mumps_par%n,1,MPI_INTEGER,0,MPI_COMM_N,ierr)

          allocate(pastix_perm_vars(mumps_par%n),pastix_iperm_vars(mumps_par%n))

          call pastix_fortran(pastix_data,MPI_COMM_N,mumps_par%n,mumps_par%jcn,mumps_par%irn,mumps_par%A, &
                              pastix_perm_vars,pastix_iperm_vars,mumps_par%rhs,1,pastix_iparm,pastix_dparm)

          pastix_initialised = .true.

        endif
      
      endif
      
    endif   ! use_pastix (initialise)


    if (use_pastix) then
    
      if ( (.not. pastix_smp_only) .or. (pastix_smp_only .and. (my_id_n .eq.0)) ) then
    
        write(*,*) my_id,my_id_n,' pastix_nthrd = ',pastix_nthrd

        pastix_iparm(2) = 1
        pastix_iparm(3) = 3
        pastix_iparm(6) = pastix_iter                 ! refinement : max number of iterations

        pastix_iparm(31) = pastix_facto
        pastix_iparm(35) = pastix_nthrd               !   numthreads   ! number of threads
        pastix_iparm(39) = pastix_rhs                 ! right hand side (0 : use RHS)
        pastix_iparm(37) = pastix_iluk
        pastix_iparm(41) = pastix_sym

        pastix_iparm(42) = pastix_ricar
        pastix_iparm(37) = pastix_iluk
        pastix_iparm(14) = pastix_amalg

        pastix_dparm(6)  = pastix_epsilon             ! error level refinement
        pastix_dparm(11) = pastix_pivot               ! pivot threshold?

        call cpu_time(t_analysis_0)

        call pastix_fortran(pastix_data,MPI_COMM_N,mumps_par%n,mumps_par%jcn,mumps_par%irn,mumps_par%A, &
                        pastix_perm_vars,pastix_iperm_vars,mumps_par%rhs,1,pastix_iparm,pastix_dparm)

        call cpu_time(t_analysis_1)

        if (my_id_n .eq.0) write(*, '(i3,A,f8.3)') my_id,' PASTIX, analysis  : ',t_analysis_1-t_analysis_0

      endif
    
    endif ! use_pastix, analysis

  endif   ! (.not., solve_only)
  
  write(*,*) ' end analysis all',my_id,my_id_n

  if (use_mumps) then

    call cpu_time(t_fact_0)

    mumps_par%JOB = 2                                   ! factorisation

    call DMUMPS(mumps_par)

    call cpu_time(t_fact_1)

    if (my_id_n .eq.0)   write(*,'(i3,A,f8.3)')  my_id,' MUMPS, fact      : ',t_fact_1-t_fact_0
    if (my_id_n .eq.0)   write(*,'(i3,A,i8)')    my_id,' MUMPS, mem       : ',mumps_par%info(16)

  elseif ( (.not. pastix_smp_only) .or. (pastix_smp_only .and. (my_id_n .eq.0)) ) then

    call cpu_time(t_fact_0)

    pastix_iparm(2) = 4
    pastix_iparm(3) = 4
    pastix_iparm(6) = pastix_iter          ! refinement : max number of iterations

    pastix_iparm(31) = pastix_facto
    pastix_iparm(35) = pastix_nthrd         ! numthreads   ! number of threads
    pastix_iparm(37) = pastix_iluk 
    pastix_iparm(39) = pastix_rhs         ! right hand side (0 : use RHS)
    pastix_iparm(41) = pastix_sym

    pastix_iparm(42) = pastix_ricar
    pastix_iparm(37) = pastix_iluk
    pastix_iparm(14) = pastix_amalg

    pastix_dparm(6)  = pastix_epsilon    ! error level refinement
    pastix_dparm(11) = pastix_pivot    ! pivot threshold?

    call pastix_fortran(pastix_data,MPI_COMM_N,mumps_par%n,mumps_par%jcn,mumps_par%irn,mumps_par%A, &
                        pastix_perm_vars,pastix_iperm_vars,mumps_par%rhs,1,pastix_iparm,pastix_dparm)

    call cpu_time(t_fact_1)

    if (my_id_n .eq.0)   write(*,'(i3,A,f8.3)')  my_id,' PastiX, fact      : ',t_fact_1-t_fact_0

  endif

endif

if (use_mumps) then

  mumps_par%JOB = 3                                   ! Solve

  call cpu_time(t_solv_0)

  call DMUMPS(mumps_par)

  call cpu_time(t_solv_1)

  if (my_id_n .eq.0)   write(*,'(i3,A,f8.3)')  my_id,' MUMPS, solv      : ',t_solv_1-t_solv_0

elseif ( (.not. pastix_smp_only) .or. (pastix_smp_only .and. (my_id_n .eq.0)) ) then

  call cpu_time(t_solv_0)

  pastix_iparm(2) = 5
  pastix_iparm(3) = pastix_endsolve
  pastix_iparm(6) = pastix_iter          ! refinement : max number of iterations

  pastix_iparm(31) = pastix_facto
  pastix_iparm(35) = pastix_nthrd ! numthreads   ! number of threads
  pastix_iparm(37) = pastix_iluk
  pastix_iparm(39) = pastix_rhs            ! right hand side (0 : use RHS)
  pastix_iparm(41) = pastix_sym

  pastix_iparm(42) = pastix_ricar
  pastix_iparm(37) = pastix_iluk
  pastix_iparm(14) = pastix_amalg

  pastix_dparm(6)  = pastix_epsilon    ! error level refinement
  pastix_dparm(11) = pastix_pivot    ! pivot threshold?

  if (.not. pastix_smp_only) call MPI_BCAST(mumps_par%rhs,mumps_par%n,MPI_DOUBLE_PRECISION,0,MPI_COMM_N,ierr)

  call pastix_fortran(pastix_data,MPI_COMM_N,mumps_par%n,mumps_par%jcn,mumps_par%irn,mumps_par%A, &
                      pastix_perm_vars,pastix_iperm_vars,mumps_par%rhs,1,pastix_iparm,pastix_dparm)

  call cpu_time(t_solv_1)

  if (my_id_n .eq.0)   write(*,'(i3,A,f8.3)')  my_id,' PastiX, solv      : ',t_solv_1-t_solv_0

endif


if (my_id_n .eq. 0) then

  if (allocated(deltas)) deallocate(deltas)
  allocate(deltas(ndof_glob))
  deltas = 0.d0

  allocate(rhs_tmp(ndof_glob))

  rhs_tmp = 0.d0

  if (my_id .eq. 0 ) then

    rhs_tmp(1:ndof_glob:n_tor) = mumps_par%rhs(1:mumps_par%n)

  else

    rhs_tmp(2*i_tor(my_id+1)-2:ndof_glob:n_tor) = mumps_par%rhs(1:mumps_par%n:2)
    rhs_tmp(2*i_tor(my_id+1)-1:ndof_glob:n_tor) = mumps_par%rhs(2:mumps_par%n:2)

  endif

  call MPI_AllReduce(RHS_tmp,deltas,ndof_glob,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_MASTER,ierr)

  deallocate(rhs_tmp)

endif

return
end
